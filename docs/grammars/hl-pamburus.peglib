#From: https://github.com/pamburus/hl/blob/0eefd5eac65e9347453c7db967316c33732b6489/src/query.pest

#input <-  SOI  ws*  _expression  ws*  EOI
input <-  ws*  _expression  ws*  EOI

_expression  <-  _e_or
expr_or      <-  _e_and  ws*  (_or  ws*  _e_and  ws*)+
expr_and     <-  _e_unary  ws*  (_and  ws*  _e_unary  ws*)+
expr_not     <-  _not  ws*  _e_unary
_e_or        <-  expr_or / _e_and
_e_and       <-  expr_and / _e_unary
_e_unary     <-  expr_not / primary
primary      <-  "("  ws*  _expression  ws*  ")" / term
term         <-  level_filter / field_filter
level_filter <-  "level"i  ws*  _lvl_op  ws*  level
field_filter <-  field_name  ws*  (_ff_rhs_num_1 / _ff_rhs_num_n / _ff_rhs_str_1 / _ff_rhs_str_n)  ws*
field_name   <-  _f_name_short / json_string

_ff_rhs_num_1 <-  _ff_num_op_1  ws*  number
_ff_rhs_num_n <-  _ff_num_op_n  ws*  number_set
_ff_rhs_str_1 <-  _ff_str_op_1  ws*  string
_ff_rhs_str_n <-  _ff_str_op_n  ws*  string_set
_f_name_short <-  <("@" / "_" / "-" / "." / LETTER / NUMBER)+>

level <-
    string  &punctuation


_or  <-  "or"i  &punctuation / "||"
_and <-  "and"i  &punctuation / "&&"
_not <-  "not"i  &punctuation / "!"

_ff_num_op_1       <-  op_le / op_ge / op_lt / op_gt
_ff_num_op_n       <-  op_in / op_not_in
_ff_str_op_1       <-  op_regex_match / op_not_regex_match / op_contain / op_not_contain / op_like / op_not_like / op_equal / op_not_equal
_ff_str_op_n       <-  op_in / op_not_in
_lvl_op            <-  op_le / op_ge / op_lt / op_gt / op_equal / op_not_equal
string_set         <-  string_set_literal / string_set_file
string_set_literal <-  "("  ws*  string  (ws*  ","  ws*  string)*  ws*  ")"
string_set_file    <-  "@"  string
number_set         <-  "("  ws*  number  (ws*  ","  ws*  number)*  ws*  ")"

op_regex_match     <-
    "~~="
  / "match"i  &punctuation

op_not_regex_match <-
    "!~~="
  / "not"i  ws+  "match"i  &punctuation

op_contain         <-
    "~="
  / "contain"i  &punctuation

op_not_contain     <-
    "!~="
  / "not"i  ws+  "contain"i  &punctuation

op_like            <-
    "like"i  &punctuation

op_not_like        <-
    "not"i  ws+  "like"i  &punctuation

op_equal           <-
    "="
  / "eq"i  &punctuation

op_not_equal       <-
    "!="
  / "not"i  ws+  "eq"i  &punctuation
  / "ne"i  &punctuation

op_in              <-
    "in"i  &punctuation

op_not_in          <-
    "not"i  ws+  "in"i  &punctuation

op_le              <-
    "<="
  / "le"i  &punctuation

op_lt              <-
    "<"
  / "lt"i  &punctuation

op_ge              <-
    ">="
  / "ge"i  &punctuation

op_gt              <-
    ">"
  / "gt"i  &punctuation


punctuation <-  "(" / ")" / ws / EOI

string <-  json_string / simple_string

json_string       <-  <"\""  json_string_inner  "\"">
json_string_inner <-  json_char*
json_char         <-
    !("\"" / "\\")  ANY
  / "\\"  ("\"" / "\\" / "/" / "b" / "f" / "n" / "r" / "t")
  / "\\"  ("u"  ASCII_HEX_DIGIT{4})


simple_string <-  <simple_char+>
simple_char   <-  (LETTER / NUMBER / "@" / "." / "_" / "-" / ":" / "/" / "!" / "#" / "%" / "$" / "*" / "+" / "?")

number <-
    <"-"?  ("0" / ASCII_NONZERO_DIGIT  ASCII_DIGIT{,19})  ("."  ASCII_DIGIT{,19})?  ("e"i  ("+" / "-")?  ASCII_DIGIT{1, 4})?>


~ws <-  (" " / "\t" / "\r" / "\n")

ANY <- .
LETTER <- [A-Za-z]
NUMBER <- [0-9]
ASCII_HEX_DIGIT <- [0-9A-Fa-f]
ASCII_NONZERO_DIGIT <- [1-9]
ASCII_DIGIT <- [0-9]
EOI <- !.