#From: https://github.com/python/cpython/blob/2f2bee21118adce653ee5bc4eb31d30327465966/Tools/peg_generator/pegen/metagrammar.gram

start <- grammar ENDMARKER

grammar <-
     meta* rules

meta <-
      "@" NAME (NAME / STRING)? #NEWLINE

rules <-
     rule+

rule <-
    # rulehead alts NEWLINE INDENT more_alts DEDENT
    #/ rulehead NEWLINE INDENT more_alts DEDENT
    #/ rulehead alts NEWLINE
    rulehead alts

rulehead <-
    rulename (PERF('(') memoflag)? ":"

rulename <-
    NAME_ANNOT annotation
    / NAME

# In the future this may return something more complicated
memoflag <-
     '(' "memo" ')'

alts <-
    "|"? alt ("|" alt)*

#more_alts <-
#     ("|" alts NEWLINE)+

alt <-
     items '$'? action?

items <-
     named_item+

named_item <-
     PERF(NAME_START) (
         NAME_ANNOT annotation '=' ↑ item
        / NAME '=' ↑ item
        )
    / item
    / PERF([&!~]) (
        forced_atom
        / lookahead
        )

forced_atom <-
     '&''&' ↑ atom

lookahead <-
     '&' ↑ atom
    / '!' ↑ atom
    / '~'

item <-
     '[' ↑ alts ']'
    /  atom (
        '?'
        / '*'
        / '+'
        / '.' atom '+'
        )?

atom <-
     '(' ↑ alts ')'
    / PERF(NAME_START) !rulehead NAME
    / PERF(['"]) STRING

# Mini-grammar for the actions and annotations

action <- <"{" ↑ target_atoms "}">
annotation <- <"[" ↑ target_atoms "]">

target_atoms <-
     target_atom+

target_atom <-
     "{" ↑ target_atoms? "}"
    / "[" ↑ target_atoms? "]"
    / NAME "*"?
    / NUMBER
    / STRING
    / "?"
    / ":"
    / !"}" !"]" OP

STRING <- <
	  '"""' (!'"""' .)* '"""'
	/ '"' (!("\\". / '"') .)* '"'
	/ "'''" (!"'''" .)* "'''"
	/ "'" (!("\\". / "'") .)* "'"
	>
NUMBER <- <[0-9]+>
~NAME_START <- [A-Za-z_]
NAME_BASE <- NAME_START [A-Za-z0-9_]*
NAME <- <NAME_BASE>
NAME_ANNOT <- <NAME_BASE &'['>
ENDMARKER <- !.
NEWLINE <- <'\n' '\r'? / '\r' '\n'?>
#INDENT <- [ \t]+
#DEDENT <- "_DEDENT_"
OP <- .

%whitespace <- ([ \t\r\n\f\v]+ / '#'(!NEWLINE .)* )*

~PERF(x) <- &x
