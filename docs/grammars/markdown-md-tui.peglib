#From: https://github.com/henriklovhaug/md-tui/blob/456ef7a18a3f18be3c5641bddae007c0f893564a/src/md.pest

txt <-
    (horizontal_sep / image / task / comment / table / quote / list_container / code_block / heading / paragraph / block_sep / WHITESPACE_S)+

# Characters
alt_char         <-  (!(NEWLINE / WHITESPACE_S / "[" / "]")  ANY)+
b_char           <-  (!(NEWLINE / comment / WHITESPACE_S / "**")  ANY)+
c_char           <-  (!(NEWLINE / WHITESPACE_S / "`")  ANY)+
c_line_char      <-  <(!(NEWLINE / "```" / "~~~")  ANY)+>
comment_char     <-  (!(NEWLINE / "-->")  ANY)+
digit            <-  '0'..'9'
i_char           <-  (!(NEWLINE / comment / WHITESPACE_S / "_" / "*")  ANY)+
indent           <-  WHITESPACE_S*
latex_char       <-  (!(NEWLINE / WHITESPACE_S / "$")  ANY)+
link_char        <-  (!(NEWLINE / WHITESPACE_S / "[" / "]" / "(" / ")")  ANY)+
p_char           <-  (!(NEWLINE / comment / code / bold_italic / italic / bold / strikethrough / latex / WHITESPACE_S / link)  ANY)+
s_char           <-  (!(NEWLINE / comment / WHITESPACE_S / "~~")  ANY)+
t_char           <-  (!(NEWLINE / comment / code / bold_italic / italic / bold / strikethrough / latex / WHITESPACE_S / "|")  ANY)+
wiki_link_char   <-  (!(NEWLINE / WHITESPACE_S / "|" / "[[" / "]]")  ANY)+
inline_link_char <-  (!(NEWLINE / WHITESPACE_S / "<" / ">")  ANY)+

# Words
word               <-
    !(forbidden_sentence_prefix / latex / bold_italic / bold / italic / strikethrough / code / link)  NEWLINE?  WHITESPACE_S*  p_char+

t_word             <-
    !(forbidden_sentence_prefix / latex / bold_italic / bold / italic / strikethrough / code / link)  NEWLINE?  WHITESPACE_S*  t_char+

alt_word           <-  WHITESPACE_S*  alt_char+
bold_italic_word   <-  WHITESPACE_S*  b_char+
bold_word          <-  WHITESPACE_S*  b_char+
code_word          <-  WHITESPACE_S*  c_char+
h_word             <-  <p_char+>  WHITESPACE_S?
italic_word        <-  WHITESPACE_S*  <i_char+>
latex_word         <-  <latex_char+>  WHITESPACE_S?
link_data          <-  link_char+
link_word          <-  (link_char / WHITESPACE_S / "(" / ")")+
strikethrough_word <-  WHITESPACE_S*  s_char+
wiki_link_alone    <-  wiki_link_char+
wiki_link_data     <-  wiki_link_char+
wiki_link_word     <-  (link_char / WHITESPACE_S)+
inline_link        <-  inline_link_char+

# Prefixes
task_open         <-  "- [ ] "
task_complete     <-  "- [x] " / "- [X] "
task_prefix       <-  task_open / task_complete
quote_prefix      <-  ">"
code_block_prefix <-  "```" / "~~~"
table_prefix      <-  "|"
list_prefix       <-  (WHITESPACE_S*  "-") / (NUMBER+  ". ")
heading_prefix    <-  "#"

forbidden_sentence_prefix <-
    NEWLINE  WHITESPACE_S*  (task_prefix / quote_prefix / code_block_prefix / table_prefix / list_prefix / heading_prefix)


# Lines
alt_text    <-  WHITESPACE_S*  alt_word+
bold        <-  NEWLINE?  WHITESPACE_S*  !"\\"  "**"  (bold_word / (NEWLINE  quote_prefix?))+  "**"
bold_italic <-
    (NEWLINE?  WHITESPACE_S*  !"\\"  "***"  (bold_italic_word / (NEWLINE  (quote_prefix  WHITESPACE_S)?))+  "***")
  / ((NEWLINE / WHITESPACE_S)  WHITESPACE_S*  !"\\"  "_**"  (bold_italic_word / (NEWLINE  quote_prefix?))+  "**_")


code                 <-  NEWLINE?  WHITESPACE_S*  ("`"  code_word+  "`") / ("```"  code_word+  "```")
code_line            <-  NEWLINE  (c_line_char / WHITESPACE_S)*
latex                <-  NEWLINE?  WHITESPACE_S*  !"\\"  "$"+  WHITESPACE_S?  (latex_word / (NEWLINE  quote_prefix?))+  "$"+
link                 <-  NEWLINE?  WHITESPACE_S*  (link_line / wiki_link / inline_link_wrapper)
link_line            <-  "["  (link_word / NEWLINE)+  "]"  "("  link_data+  ")"
inline_link_wrapper  <-  !comment  "<"  inline_link  ">"
wiki_link            <-  ("[["  wiki_link_alone+  "]]") / ("[["  wiki_link_data+  "|"  wiki_link_word+  "]]")
normal               <-  <word+>
o_list_counter       <-  digit+  ". "
programming_language <-  <(!NEWLINE  ANY)+>
strikethrough        <-  NEWLINE?  WHITESPACE_S*  !"\\"  "~~"  !"~"  (strikethrough_word / (NEWLINE  quote_prefix?))+  "~~"
t_normal             <-  t_word+

italic <-
    (NEWLINE?  WHITESPACE_S*  !"\\"  "*"  (italic_word / (NEWLINE  quote_prefix?))+  "*")
  / ((NEWLINE / WHITESPACE_S)  WHITESPACE_S*  !"\\"  "_"  (italic_word / (NEWLINE  quote_prefix?))+  "_")


sentence   <-  (latex / code / link / bold_italic / italic / bold / strikethrough / normal+)+
t_sentence <-  (!"|"  (latex / code / link / italic / bold / strikethrough / t_normal))+

table_cell      <-  "|"  WHITESPACE_S*  t_sentence*  WHITESPACE_S*  ("|"  " "*  NEWLINE)?
table_seperator <-  ("|"?  (WHITESPACE_S / ":")*  "-"+  (WHITESPACE_S / ":")*  "|")

u_list <-  indent  ("-" / "*")  WHITESPACE_S  sentence+
o_list <-  indent  o_list_counter  sentence+

# Headings
h1 <-  "# "  (h_word / WHITESPACE_S)+
h2 <-  "## "  (h_word / WHITESPACE_S)+
h3 <-  "### "  (h_word / WHITESPACE_S)+
h4 <-  "#### "  (h_word / WHITESPACE_S)+
h5 <-  "##### "  (h_word / WHITESPACE_S)+
h6 <-  "###### "  (h_word / WHITESPACE_S)+

# Quote markings
important <-  "[!important]"i
note      <-  "[!note]"i
tip       <-  "[!tip]"i
warning   <-  "[!warning]"i
caution   <-  "[!caution]"i

quote_marking <-  WHITESPACE_S*  (important / note / tip / warning / caution)

# Blocks
heading        <-  (h1 / h2 / h3 / h4 / h5 / h6)
list_container <-  (NEWLINE?  !comment  (u_list / o_list))+
paragraph      <-  sentence+
code_block     <-
    NEWLINE?  " "*  (("```"  programming_language?  code_line+  " "*  "```") / ("~~~"  programming_language?  code_line+  " "*  "~~~"))

table          <-  (NEWLINE?  (table_seperator / table_cell))+
quote          <-  (NEWLINE?  WHITESPACE_S*  ">"  ((quote_marking / sentence / " ")+ / NEWLINE))+
task           <-  NEWLINE?  task_prefix  sentence
block_sep      <-  NEWLINE
horizontal_sep <-  NEWLINE?  "---"  "-"*
image          <-  NEWLINE?  "!["  alt_text  "]("  link_data+  ")"

comment <-  "<!--"  (NEWLINE / comment_char)+  "-->"

~WHITESPACE_S <-  " " / "\t"

NEWLINE <- '\n' '\r'? / '\r' '\n'?
NUMBER <- [0-9]
ANY <- .
