#From: https://github.com/cloudwebrtc/peg/blob/c448cec7e7a0601fde427fdf7bba0cafb89425b4/README.md?plain=1#L193

Grammar <- LEADING_SPACES? GLOBALS? MEMBERS? Definition+ EOF

Definition <- IDENTIFIER LEFTARROW Expression

Expression <- Sequence (SLASH Sequence)*

Sequence <- Prefix+

Prefix <- (AND / NOT)? Suffix ACTION?

Suffix <- Primary (QUESTION / STAR / PLUS)?

Primary <- IDENTIFIER !LEFTARROW / OPEN Expression CLOSE / LITERAL / CLASS / DOT

ACTION <- <'{' ACTION_BODY* '}'> SPACING

AND <- '&' SPACING

CLASS <- <'[' (!']' RANGE)* ']'> SPACING

CLOSE <- ')' SPACING

DOT <- '.' SPACING

EOF <- !.

GLOBALS <- <'%{' GLOBALS_BODY* '}%'> SPACING

IDENTIFIER <- <IDENT_START IDENT_CONT*> SPACING

~LEADING_SPACES <- SPACING

LEFTARROW <- '<-' SPACING

LITERAL <- <'\'' (!'\'' CHAR)* '\''> SPACING / <'"' (!'"' CHAR)* '"'> SPACING

MEMBERS <- '{' ACTION_BODY* '}' SPACING

NOT <- '!' SPACING

OPEN <- '(' SPACING

PLUS <- '+' SPACING

QUESTION <- '?' SPACING

SLASH <- '/' SPACING

STAR <- '*' SPACING

ACTION_BODY <- '{' ACTION_BODY* '}' / !'}' .

CHAR <- '\\' [-"'\[-\]nrt] / HEX_NUMBER / !'\\' !EOL .

COMMENT <- '#' (!EOL .)* EOL?

EOL <- '\r\n' / [\n\r]

GLOBALS_BODY <- !'}%' .

HEX_NUMBER <- [\\] 'u' [0-9A-Fa-f]+

IDENT_CONT <- IDENT_START / [0-9]

IDENT_START <- [A-Z_a-z]

RANGE <- CHAR '-' CHAR / CHAR

SPACE <- [\t ] / EOL

~SPACING <- (SPACE / COMMENT)*